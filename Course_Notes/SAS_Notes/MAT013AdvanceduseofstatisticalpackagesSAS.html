<h1 id="mat013-advanced-use-of-statistical-packages---sas">MAT013 Advanced use of statistical packages - SAS</h1>
<p>Chapter 1 - Introduction</p>
<ol style="list-style-type: decimal">
<li>The environment</li>
<li>Libraries</li>
<li>Importing data</li>
<li>Exporting data</li>
</ol>
<p>Chapter 2 - Basic Statistical Procedures</p>
<ol style="list-style-type: decimal">
<li>Procedures</li>
<li>A list of procedures</li>
<li>Exporting output</li>
</ol>
<p>Chapter 3 - Manipulating Data</p>
<ol style="list-style-type: decimal">
<li>Data steps</li>
<li>The program data vector</li>
<li>Creating new variables</li>
<li>Handling dates</li>
</ol>
<p>Chapter 4 - Programming</p>
<ol style="list-style-type: decimal">
<li>Flow control</li>
<li>SAS Macro compiler</li>
<li>Global and local macro variables</li>
<li>Macro Programming Statements</li>
<li>Macro Functions</li>
</ol>
<p>Chapter 5 - Further procs</p>
<ol style="list-style-type: decimal">
<li>SQL</li>
<li>Functions</li>
<li>Optimisation</li>
</ol>
<hr />
<h1 id="chapter-1---introduction">Chapter 1 - Introduction</h1>
<hr />
<h2 id="the-environment">1.1 The Environment</h2>
<p>SAS may be run in a variety of modes, on this course we will concentrate on the interactive mode which allows users to submit selected portions of SAS code through a graphical user interface (GUI). When opening SAS a variety of windows immediately become visible as shown. Note that the screenshots and accompanying screen casts for this course were produced with SAS 9.3 running on ubuntu 11.10. The look and feel on other operating systems will differ slightly.</p>
<div class="figure">
<img src="images/image21.png" alt="The SAS Environment" /><p class="caption">The SAS Environment</p>
</div>
<p>The visible windows are:</p>
<ol style="list-style-type: decimal">
<li>The explorer window</li>
<li>The results window</li>
<li>The command window</li>
<li>The output window</li>
<li>The log window</li>
<li>The editor window</li>
</ol>
<p>We write code directly in the editor window and the roles of the other windows will become clear shortly.</p>
<h2 id="libraries">1.2 Libraries</h2>
<p>The major strength of SAS is its ability to handle huge data sets. SAS does this by storing files in a particular format in spaces called libraries. SAS libraries are important. SAS manipulates data sets once they are converted to SAS data files. These data files are saved in libraries in SAS. They work just like folders (apart from not being able to nest further libraries). If you click on the libraries tab in the explorer window (as shown in in the screenshot) you should see the libraries available to you (as shown in the other screenshot).</p>
<div class="figure">
<img src="images/image58.png" alt="A closer look at the explorer window" /><p class="caption">A closer look at the explorer window</p>
</div>
<div class="figure">
<img src="images/image32.png" alt="The original set of libraries on a linux install" /><p class="caption">The original set of libraries on a linux install</p>
</div>
<p>On my system SAS has already created 6 libraries (this might differ on other versions and operating systems). The Work library which SAS automatically uses if no library is specified (more on this later, it's basically the default library). A very important fact about the Work library is that it is temporary. When SAS is shut down, all the contents of the Work library are deleted. Keeping this in mind, let's move on to creating a new library.</p>
<h3 id="creating-a-new-library">Creating a new library</h3>
<p>To create a new library, left click in the explorer window and select &quot;New...&quot;. You will see a new window appear as shown. Simply browse to the location on your computer at which you'd like your new library to be stored. Note also to click the &quot;Enable at startup&quot; option which ensures that SAS remembers this library the next time you open up SAS; if this is not selected, the link to the library created will be temporary (and erased when SAS is shut down). Finally make sure you name your library obeying the following rules (for the rest of the notes, I'll assume the library name for this course is <code>mat013</code>):</p>
<ol style="list-style-type: decimal">
<li>be less than or equal to 8 characters</li>
<li>must begin with an underscore or letter</li>
<li>remaining characters can be letters, numbers or underscores</li>
</ol>
<div class="figure">
<img src="images/image57.png" alt="New library window" /><p class="caption">New library window</p>
</div>
<p>Now that we have a library let's import some data!</p>
<h2 id="importing-data">1.3 Importing Data</h2>
<p>There are two main ways to import data into SAS:</p>
<ol style="list-style-type: decimal">
<li>Direct input</li>
<li>Importing an external data set (xls, csv etc...)</li>
</ol>
<p>In practice you will never use the direct input method but let's take a look for completeness (although it is very useful when wanting to quickly test a few things). This will also give us our first experience of the editor window!</p>
<p>Let us create a data set named &quot;first_data_set&quot;, put it in the &quot;mat013&quot; library and include the following data:</p>
<pre><code>Name,Age
Bob,23
Billy,25</code></pre>
<p>To do so, write the following code in the editor window:</p>
<pre><code>data mat013.first_data_set;
input Name $ Age;
cards;
Bob 23
Billy 25
;
run;</code></pre>
<p>Let's take a look at the screenshot. First of all we see that the program editor automatically includes some syntax colouring (i.e. changes the colour of some of the words that it recognises). In blue in the editor window are the SAS keywords:</p>
<ol style="list-style-type: decimal">
<li>&quot;data&quot; which tells SAS that we're about to write a &quot;data step&quot; which we'll look at a bit closer in the Chapter 3. The keyword data is always followed by the library and the data file (separated by a &quot;.&quot;) we're creating. If no library is given then SAS will put this file in the Work library.</li>
<li>&quot;input&quot; which tells SAS that we're going to input raw data and what follows is the name of the variables. If a variable is a string then we must include a &quot;$&quot; after the variable name.</li>
<li>&quot;cards&quot; which is the SAS keyword that precedes the raw data. All the entries must be on separate rows.</li>
<li>&quot;run&quot; which is the keyword that tells SAS where the statement ends.</li>
</ol>
<p>An important thing to remember is that a SAS statement always ends with a &quot;;&quot;. Forgetting the &quot;;&quot; is a common source of mistakes (and headaches).</p>
<div class="figure">
<img src="images/image60.png" alt="A short program to directly input data in to SAS." /><p class="caption">A short program to directly input data in to SAS.</p>
</div>
<p>We run this code by highlighting it and pressing the 'running man', clicking on run (or pressing F8 on Windows). It is good practice to always check the log window as soon as any code is run. In the screenshot we see that the log looks good (lines 1-7 don't show any errors) and simply gives some details as to the running of the program.</p>
<div class="figure">
<img src="images/image35.png" alt="A short program to directly input data in to SAS." /><p class="caption">A short program to directly input data in to SAS.</p>
</div>
<p>If we now look at the mat013 library in the explorer pane we can see the new data set is in there, double clicking on the data set opens it up.</p>
<div class="figure">
<img src="images/image26.png" alt="The explorer pane with our first data set." /><p class="caption">The explorer pane with our first data set.</p>
</div>
<div class="figure">
<img src="images/image42.png" alt="Viewing our first data set." /><p class="caption">Viewing our first data set.</p>
</div>
<p>Using direct input is of course not at all realistic when trying to import larger data sets.</p>
<p>Often large data sets will be saved in comma-separated values (csv) format which can be read by most (all?) software. We will import the data set shown (here viewed in a simple text editor).</p>
<div class="figure">
<img src="images/image13.png" alt="The JJJ.csv data set" /><p class="caption">The JJJ.csv data set</p>
</div>
<p>We will import this data set in to the mat013 library and call it JJJ using the following code:</p>
<pre><code>proc import datafile=&quot;~/JJJ.csv&quot;
    out=mat013.JJJ
    dbms=csv
    replace;
    getnames=yes;
run;</code></pre>
<p>Let's take a look at the screenshot shown. We again see that the program editor automatically includes some syntax colouring (i.e. changes the colour of some of the words that it recognises). In blue in the editor window are the SAS keywords:</p>
<ol style="list-style-type: decimal">
<li>&quot;proc&quot; which tells SAS that we're about to write a &quot;procedure step&quot; which we'll look at a bit closer in the next chapter. The &quot;proc&quot; keyword is always followed by the name of the particular procedure we're going to use. In this case: &quot;import&quot;, which is then followed by the statement &quot;datafile=path-to-datafile&quot;. Following this are various options relating to the import statement.</li>
<li>&quot;out&quot; - this tells SAS the name of the SAS datafile created from the imported file.</li>
<li>&quot;dbms&quot; - this tells SAS the type of file being imported (in our case csv, but can be &quot;dlm&quot;, &quot;xls&quot;, etc.). Note that this is not necessary if SAS can recognise the file extension.</li>
<li>&quot;replace&quot; - this tells SAS to replace any SAS datafiles with the same name as specified by &quot;out&quot;.</li>
<li>&quot;getnames=yes&quot; which, although this is not a SAS keyword, it is a special option for the import statement that allows you to tell SAS to get the variable names from the first row of your external data file.</li>
<li>&quot;run&quot; is the keyword that tells SAS where the statement ends.</li>
</ol>
<div class="figure">
<img src="images/image62.png" alt="A short program to import a csv file in to SAS." /><p class="caption">A short program to import a csv file in to SAS.</p>
</div>
<p>Running the code in the same way as before (highlighting and F8) will create the required datafile as shown.</p>
<div class="figure">
<img src="images/image05.png" alt="The explorer pane with our JJJ data set." /><p class="caption">The explorer pane with our JJJ data set.</p>
</div>
<div class="figure">
<img src="images/image31.png" alt="Viewing the JJJ data set." /><p class="caption">Viewing the JJJ data set.</p>
</div>
<p>In the following chapters we will learn how to create new data sets from old data sets and as such it may become necessary to export files to csv.</p>
<h2 id="exporting-data-sets">1.3 Exporting data sets</h2>
<p>We will export our first data set (&quot;mat013.first_dataset&quot;) to csv using the following code:</p>
<pre><code>proc export data=mat013.first_data_set
    outfile=&quot;~/Desktop/first_data_set.csv&quot;
    dbms=csv
    replace;
run;</code></pre>
<p>Let's take a look at the screenshot shown. In blue are the SAS keywords:</p>
<ol style="list-style-type: decimal">
<li>&quot;proc&quot; which tells SAS that we're about to write a &quot;procedure step&quot; which we'll look at a bit closer in the next chapter. The &quot;proc&quot; keyword is always followed by the name of the particular procedure we're going to use. In this case: &quot;export&quot;, which is then followed by the statement &quot;data=&quot; followed by the library and name of the SAS data file you want to export. Following this are various options relating to the export statement.</li>
<li>&quot;outfile&quot; - this tells SAS where the exported file should go.</li>
<li>&quot;dbms&quot; - this tells SAS the type of file to create when exporting (in our case csv, but can be &quot;dlm&quot;, &quot;xls&quot;, etc...). Note that this is not necessary if SAS can recognise the file extension.</li>
<li>&quot;replace&quot; - this tells SAS to replace any file with the same name as specified by &quot;outfile&quot;.</li>
<li>&quot;run&quot; is the keyword that tells SAS where the statement ends.</li>
</ol>
<div class="figure">
<img src="images/image56.png" alt="A short program to export a SAS data file to csv" /><p class="caption">A short program to export a SAS data file to csv</p>
</div>
<p>In the next chapter we will see more complex (and potentially useful) procedures.</p>
<hr />
<h1 id="chapter-2---basic-statistical-procedures">Chapter 2 - Basic Statistical Procedures</h1>
<hr />
<h2 id="procedures">2.1 Procedures</h2>
<p>In the previous chapter we were introduced to some very basic aspects of SAS:</p>
<ol style="list-style-type: decimal">
<li>what SAS looks like</li>
<li>how to import data into SAS</li>
<li>how to export data from SAS</li>
</ol>
<p>In this chapter we will take a closer look at &quot;procedure steps&quot; which allow us to call a SAS procedure to analyse or process a SAS dataset. In the previous chapter we have already seen two procedure steps:</p>
<ol style="list-style-type: decimal">
<li>proc import</li>
<li>proc export</li>
</ol>
<p>The procedures we are going to look at in this chapter are:</p>
<ol style="list-style-type: decimal">
<li>Viewing datasets</li>
<li>Summarising the contents of data sets</li>
<li>Obtaining summary statistics of data sets</li>
<li>Obtaining frequency tables</li>
<li>Obtaining linear models</li>
<li>Plotting data</li>
</ol>
<p>The general syntax for these procedures in SAS is given below:</p>
<pre><code>proc [NAME OF PROCEDURE] data=[NAME OF SAS DATA SET];
[Options for Procedure being used]
run;</code></pre>
<p>Some of the options that can be used in a procedure step include:</p>
<ol style="list-style-type: decimal">
<li>&quot;var&quot; - which tells SAS which variables are to be processed.</li>
<li>&quot;by&quot; - which tells SAS to compartementalize the procedure for each different value of the named variable(s). The data set must first be sorted by those variables.</li>
<li>&quot;where&quot; - select only those observations for which the expression is true.</li>
</ol>
<h2 id="a-list-of-procedures">2.2 A list of procedures</h2>
<h3 id="utility-procedures">Utility procedures</h3>
<p>We have already seen that we can open and view a data set by simply double clicking on the data set in the explorer window. A data set can also be viewed by using the &quot;print&quot; procedure.</p>
<p>We'll do this by considering the MMM data file shown (imported using an import procedure).</p>
<div class="figure">
<img src="images/image41.png" alt="The mat013.mmm data set." /><p class="caption">The mat013.mmm data set.</p>
</div>
<p>The following code will run the &quot;print&quot; procedure:</p>
<pre><code>proc print data=mat013.mmm;
run;</code></pre>
<p>which outputs the data set to the output window as shown.</p>
<div class="figure">
<img src="images/image24.png" alt="The mat013.mmm shown using the &quot;print&quot; procedure." /><p class="caption">The mat013.mmm shown using the &quot;print&quot; procedure.</p>
</div>
<p>At times we might not want to open the data set but simply gain some information as to what is in the data set. This is equivalent to checking the label on a present without unwrapping it. We do this using the &quot;contents&quot; procedure.</p>
<pre><code>proc contents data=mat013.mmm;
run;</code></pre>
<p>This outputs summary information as shown.</p>
<div class="figure">
<img src="images/image59.png" alt="Summary information regarding mat013.mmm viewed using the contents procedure." /><p class="caption">Summary information regarding mat013.mmm viewed using the contents procedure.</p>
</div>
<p>A procedure that will be needed, when using more complex procedures and larger data sets, is the &quot;sort&quot; procedure.</p>
<pre><code>proc sort data=mat013.mmm;
by age;
run;</code></pre>
<p>Note that this procedure makes use of the &quot;by&quot; statement which tells SAS which variable to sort our observations on (in this case the variable age). Recall that the data set is not sorted. If we run the above &quot;sort&quot; procedure, at first nothing seems to happen, however if we view the data set again (using proc print or otherwise) we see (as shown) that the data set is now sorted.</p>
<div class="figure">
<img src="images/image06.png" alt="The mat013.mmm sorted by the Age variable." /><p class="caption">The mat013.mmm sorted by the Age variable.</p>
</div>
<p>Important: If you have the mat013.mmm data set open in browser mode (i.e. having double clicked on the data set in the explorer window) when running the &quot;sort&quot; procedure, checking your log shows you an error as shown. Always close any browser windows when processing a data set - or use the &quot;print&quot; procedure!</p>
<div class="figure">
<img src="images/image20.png" alt="Error associated with trying to manipulate a data set that is open in browser mode." /><p class="caption">Error associated with trying to manipulate a data set that is open in browser mode.</p>
</div>
<h3 id="descriptive-statistics">Descriptive statistics</h3>
<p>In this section we will go over some of the procedures needed to obtain descriptive statistics.</p>
<p>The first procedure we consider is the &quot;means&quot; procedure. We can use the following code to obtain various summary statistics relating to the age variables of the mat013.mmm dataset.</p>
<pre><code>proc means data=mat013.mmm;
var age;
run;</code></pre>
<p>We can specify the particular summary statistics we want (if none are specified a default set is displayed).</p>
<pre><code>proc means data=mat013.mmm N mean std min max sum var css uss;
var age;
run;</code></pre>
<p>We can also choose to display the summary statistics for more than one variable</p>
<pre><code>proc means data=mat013.mmm N mean std min max sum var css uss;
var age height_in_metres;
run;</code></pre>
<p>We can compartmentalise our data results using the &quot;by&quot; statement. Note that the data set must be sorted on the same variable.</p>
<pre><code>proc means data=mat013.mmm N mean std min max sum var css uss;
var age height_in_metres;
by sex;
run;</code></pre>
<p>Another way of compartmentalising results is using the &quot;class&quot; statement. This is very similar to the &quot;by&quot; statement and does not require the prior sorting of your data set.</p>
<pre><code>proc means data=mat013.mmm N mean std min max sum var css uss;
var age height_in_metres;
class sex;
run;</code></pre>
<p>Finally, it's also possible to create a data set from the &quot;means&quot; procedure.</p>
<pre><code>proc means data=mat013.mmm N mean;
var age height_in_metres;
class sex;
output out=summary_of_mmm
N(age)=number_of_age_obs
mean(age)=average_of_age_obs
N(height_in_metres)=number_of_height_obs
mean(height_in_metres)=average_height;
run;</code></pre>
<p>The above code creates a data set called &quot;summary_of_mmm&quot; in the work library (the default library if no library is specified) with two variables &quot;number_of_obs&quot; and &quot;average_of_obs&quot; which give the number and mean for the observations as calculated by the &quot;means&quot; procedure as shown.</p>
<div class="figure">
<img src="images/image16.png" alt="Data set created using the &quot;means&quot; procedure." /><p class="caption">Data set created using the &quot;means&quot; procedure.</p>
</div>
<p>The &quot;univariate&quot; procedure allows for the calculation of univariate statistics in SAS. The following code will output all the default univariate statistics for all the variables.</p>
<pre><code>proc univariate data=mat013.mmm;
run;</code></pre>
<p>We can choose to run the &quot;univariate&quot; procedure on a subset of the variables, using the &quot;var&quot; statement.</p>
<pre><code>proc univariate data=mat013.mmm;
var savings_in_pounds;
run;</code></pre>
<p>The various outputs of the &quot;univariate&quot; procedure are shown.</p>
<div class="figure">
<img src="images/image10.png" alt="Moments calculated using proc univariate." /><p class="caption">Moments calculated using proc univariate.</p>
</div>
<div class="figure">
<img src="images/image03.png" alt="Basic measures of location calculated using proc univariate." /><p class="caption">Basic measures of location calculated using proc univariate.</p>
</div>
<div class="figure">
<img src="images/image61.png" alt="Tests for location calculated using proc univariate." /><p class="caption">Tests for location calculated using proc univariate.</p>
</div>
<div class="figure">
<img src="images/image53.png" alt="Quantiles calculated using proc univariate." /><p class="caption">Quantiles calculated using proc univariate.</p>
</div>
<div class="figure">
<img src="images/image36.png" alt="Extreme values calculated using proc univariate." /><p class="caption">Extreme values calculated using proc univariate.</p>
</div>
<h3 id="frequency-tables">Frequency tables</h3>
<p>The &quot;freq&quot; procedure allows us to obtain frequency tables of data sets. As an example, let's consider the dataset shown.</p>
<div class="figure">
<img src="images/image27.png" alt="The data set Math_tests.csv" /><p class="caption">The data set Math_tests.csv</p>
</div>
<p>The most basic &quot;freq&quot; procedure will give the frequencies of all the observations in the data set:</p>
<pre><code>proc freq data=mat013.math_tests;
run;</code></pre>
<p>We can specify the variables we want to look at by listing them after the &quot;tables&quot; statement (similar to the var statement for the &quot;means&quot; procedure):</p>
<pre><code>proc freq data=mat013.math_tests;
tables teacher pass_fail;
run;</code></pre>
<p>If we want to cross tabulate the data then we use a <code>*</code> in between the variables concerned:</p>
<pre><code>proc freq data=mat013.math_tests;
tables teacher*pass_fail;
run;</code></pre>
<p>The above code gives the table shown.</p>
<div class="figure">
<img src="images/image08.png" alt="Frequency table for the math_tests data set." /><p class="caption">Frequency table for the math_tests data set.</p>
</div>
<p>Various options can be passed to the &quot;freq&quot; procedure, the simplest of which is shown below:</p>
<pre><code>proc freq data=mat013.math_tests;
tables teacher*pass_fail / nocol norow nopercent;
run;</code></pre>
<p>Other options include computing a chi square test but we will not worry about that for now.</p>
<h3 id="correlations">Correlations</h3>
<p>The &quot;corr&quot; procedure can be used to obtain correlations in SAS. The following code is the basic &quot;corr&quot; procedure applied to the mat013.mmm data set which gives the output shown.</p>
<pre><code>proc corr data=mat013.mmm;
run;</code></pre>
<div class="figure">
<img src="images/image49.png" alt="Output of the &quot;corr&quot; procedure acting on all the variables of the mat013.mmm data set." /><p class="caption">Output of the &quot;corr&quot; procedure acting on all the variables of the mat013.mmm data set.</p>
</div>
<p>If we want to run the &quot;corr&quot; procedure on a subset of the variables then we use the &quot;var&quot; statement:</p>
<pre><code>proc corr data=mat013.mmm;
var age savings_in_pounds;
run;</code></pre>
<div class="figure">
<img src="images/image22.png" alt="Output of the &quot;corr&quot; procedure acting on a subset of the mat013.mmm data set." /><p class="caption">Output of the &quot;corr&quot; procedure acting on a subset of the mat013.mmm data set.</p>
</div>
<h3 id="linear-models">Linear Models</h3>
<p>In this section we'll very briefly see the syntax for some basic linear models in SAS. First of all we'll take a look at linear regression. The following code will run such an analysis on the mat013.jjj data set, checking if there is a linear model of height with predictors weight and savings:</p>
<pre><code>proc reg data=mat013.jjj;
model height_in_metres=weight_in_kg savings_in_pounds;
run;</code></pre>
<div class="figure">
<img src="images/image12.png" alt="Overall regression results" /><p class="caption">Overall regression results</p>
</div>
<div class="figure">
<img src="images/image39.png" alt="Detailed regression results" /><p class="caption">Detailed regression results</p>
</div>
<p>Looking at the p-value we see that the overall model should not be rejected, however the detailed results show that perhaps we could remove savings from the model.</p>
<p>Analysis of variance (ANOVA) can be done very easily in SAS. We show this using a new data set.</p>
<div class="figure">
<img src="images/image09.png" alt="The math.csv data set" /><p class="caption">The math.csv data set</p>
</div>
<p>We will use the &quot;anova&quot; procedure to see if the grades obtained by students depend on their teacher.</p>
<pre><code>proc anova data=mat013.math;
class prof;
model grade=prof;
run;</code></pre>
<p>Note the &quot;class&quot; keyword is needed to state which variable we are using to group on. The results show that there is indeed a difference between groups (further post-hoc tests are needed to investigate which groups differ etc.).</p>
<div class="figure">
<img src="images/image25.png" alt="Overall Results from the ANOVA." /><p class="caption">Overall Results from the ANOVA.</p>
</div>
<p>Another procedure that can be used for a variety of models (including the 2-way anova) is the &quot;glm&quot; (general linear model) procedure. The following code simply reproduces the above results.</p>
<pre><code>proc glm data=mat013.jjj;
model height_in_metres=weight_in_kg savings_in_pounds;
run;

proc glm data=mat013.math;
class prof;
model grade=prof;
run;</code></pre>
<h3 id="plots-and-charts">Plots and charts</h3>
<p>There are various ways to obtain histograms in SAS, the easiest way is to use the &quot;univariate&quot; procedure with the &quot;histogram&quot; option. The following code gives a histogram for the height of individuals in the mat013.jjj dataset as shown.</p>
<pre><code>proc univariate data=mat013.jjj;
var height_in_metres;
histogram;
run;</code></pre>
<div class="figure">
<img src="images/image01.png" alt="A histogram obtained using the &quot;univariate&quot; procedure." /><p class="caption">A histogram obtained using the &quot;univariate&quot; procedure.</p>
</div>
<p>There are various ways to obtain scatter plots in SAS, the easiest way is to use the &quot;gplot&quot; procedure. The following code gives a scatter plot for the height of individuals against their weight in the mat013.jjj dataset as shown.</p>
<pre><code>proc gplot data=mat013.jjj;
plot height_in_metres*weight_in_kg;
run;</code></pre>
<div class="figure">
<img src="images/image28.png" alt="A scatter obtained using the &quot;gplot&quot; procedure." /><p class="caption">A scatter obtained using the &quot;gplot&quot; procedure.</p>
</div>
<p>There are various other ways to obtain similar graphs as well as change the look and feel of our graphs. We won't go into this here but you are encouraged to look into it.</p>
<h2 id="exporting-output">2.3 Exporting output</h2>
<p>We can output results of procedures in SAS using the &quot;output delivery system&quot;. The syntax is straightforward and we surround normal SAS code with the &quot;ods&quot; statements to output to various formats (html, pdf, rtf).</p>
<pre><code>ods [format of your choice] file=[Location of file to be output];
[Normal SAS code]
ods [format of your choice] close;</code></pre>
<p>As an example, the following code creates an html file called &quot;freq_table&quot; in html format stored at the location &quot;~/Desktop&quot; (note that in Window's the <code>/</code> should be a <code>\</code>) as shown.</p>
<pre><code>ods html file=&quot;\~/Desktop/freq_table.htm&quot;;

proc gplot data=mat013.jjj;
plot height_in_metres*weight_in_kg;
run;

ods html close;</code></pre>
<div class="figure">
<img src="images/image17.png" alt="Ods output in html format." /><p class="caption">Ods output in html format.</p>
</div>
<p>The following code will create a file called &quot;scatter_plot.pdf&quot; in pdf format stored at the location &quot;~/Desktop&quot; (note that in Window's the &quot;/&quot; should be a &quot;&quot;) as shown.</p>
<pre><code>ods pdf file=&quot;\~/Desktop/scatter_plot.pdf&quot;;

proc gplot data=mat013.jjj;
plot height_in_metres*weight_in_kg;
run;

ods pdf close;</code></pre>
<div class="figure">
<img src="images/image02.png" alt="Ods output in pdf format." /><p class="caption">Ods output in pdf format.</p>
</div>
<p>The following code will create a file called &quot;regression.rtf&quot; in rtf format (Word, LibreOffice etc.) stored at the location &quot;~/Desktop&quot; (note that in Window's the &quot;/&quot; should be a &quot;&quot;) as shown.</p>
<pre><code>ods rtf file=&quot;\~/Desktop/regression.rtf&quot;;

proc reg data=mat013.jjj;
model weight_in_kg=height_in_metres;
run;

ods rtf close;</code></pre>
<div class="figure">
<img src="images/image55.png" alt="Ods output in rtf format." /><p class="caption">Ods output in rtf format.</p>
</div>
<hr />
<h1 id="chapter-3---manipulating-data">Chapter 3 - Manipulating data</h1>
<hr />
<h2 id="data-steps">3.1 Data steps</h2>
<p>A data step is a type of SAS statement that allows you to manipulate SAS data sets. Some of the things we can do include:</p>
<ol style="list-style-type: decimal">
<li>Copying a data set (with new variables)</li>
<li>Concatenating any number of data sets</li>
<li>Merging any number of data sets</li>
</ol>
<p>The following code simply creates a data set in the work library called &quot;J&quot; that is a copy of the data set jjj located in the mat013 library.</p>
<pre><code>data j;
set mat013.jjj;
run;</code></pre>
<p>To concatenate two data sets (as shown pictorially) we use the following syntax:</p>
<pre><code>data [New Data Set];
set A B;
run;</code></pre>
<div class="figure">
<img src="images/image00.png" alt="Concatenating two data sets." /><p class="caption">Concatenating two data sets.</p>
</div>
<p>The following code concatenates the jjj and mmm data sets as shown.</p>
<pre><code>data mat013.mmmjjj;
set mat013.mmm mat013.jjj;
run;</code></pre>
<div class="figure">
<img src="images/image54.png" alt="A concatenated data set." /><p class="caption">A concatenated data set.</p>
</div>
<p>To merge two data sets (as shown pictorially) we use the following syntax:</p>
<pre><code>data [New Data Set];
merge A B;
by [Merge Variable]
run;</code></pre>
<p>Note that the two data sets must be sorted on the merge variable prior to merging.</p>
<div class="figure">
<img src="images/image19.png" alt="Merging two data sets." /><p class="caption">Merging two data sets.</p>
</div>
<p>The following code would merge the two data sets first_data_set and other_data_set in the mat013 library as shown.</p>
<pre><code>proc sort data=mat013.first_data_set;
by name;
run;

proc sort data=mat013.other_data_set;
by name;
run;

data mat013.merged_data_set;
merge mat013.first_data_set mat013.other_data_set;
by name;
run;</code></pre>
<div class="figure">
<img src="images/image34.png" alt="A merged data set." /><p class="caption">A merged data set.</p>
</div>
<p>Data steps can be used in conjunction with the &quot;where&quot; statement to select certain variables. For example consider the data set shown.</p>
<div class="figure">
<img src="images/image11.png" alt="The Dwarfs data set." /><p class="caption">The Dwarfs data set.</p>
</div>
<p>The following code selects only the elements of the above data set that start with a &quot;D&quot;.</p>
<pre><code>data Dwarfs;
set Dwarfs;
where substr(Name,1,1)=&quot;D&quot;;
run;</code></pre>
<p>The result is shown in (note that the above code makes use of the &quot;substr&quot; function that we will see later).</p>
<div class="figure">
<img src="images/image45.png" alt="Elements of the Dwarfs data set starting with &quot;D&quot;." /><p class="caption">Elements of the Dwarfs data set starting with &quot;D&quot;.</p>
</div>
<h2 id="the-program-data-vector">3.2 The program data vector</h2>
<p>SAS is able to handle very large data sets because of the way data steps work. In this section we'll explain how it uses the &quot;program data vector&quot; (pdv) to efficiently handle data. The basic steps of compiling a data step are as follows:</p>
<ol style="list-style-type: decimal">
<li>SAS creates an empty data set.</li>
<li>SAS checks the data step for any unrecognized keywords and syntax errors.</li>
<li>SAS creates a PDV to store the information for all the variables required from the data step.</li>
<li>SAS reads in the data line by line using the PDF.</li>
</ol>
<p>(If a &quot;by&quot; statement is used (for example when merging two data sets) the PDF does not empty if there are still observations with the same value of the &quot;by&quot; variable).</p>
<ol start="5" style="list-style-type: decimal">
<li>SAS creates the descriptive portion of the SAS data set (viewable using the &quot;contents&quot; procedure).</li>
</ol>
<p>An example of how this works with concatenation and an example of how this works with merging is shown.</p>
<div class="figure">
<img src="images/image15.png" alt="Concatenation of data sets and the pdv." /><p class="caption">Concatenation of data sets and the pdv.</p>
</div>
<div class="figure">
<img src="images/image30.png" alt="Merging of data sets and the pdv." /><p class="caption">Merging of data sets and the pdv.</p>
</div>
<h2 id="creating-new-variables">3.3 Creating new variables</h2>
<p>Creating new variables using various arithmetic and/or string relationships is relatively straightforward in SAS. The following code creates a new data set call MMM_with_BMI, with a new variable &quot;BMI&quot; as a function of the height and weight variables in the MMM dataset in the mat013 library.</p>
<pre><code>data mat013.MMM_with_BMI;
set mat013.MMM;
bmi=weight_in_kg/(height_in_metres**2);
run;</code></pre>
<p>Some of the arithmetic functions are shown.</p>
<div class="figure">
<img src="images/SAS_math_operators.png" alt="Basic arithmetic operations in SAS" /><p class="caption">Basic arithmetic operations in SAS</p>
</div>
<div class="figure">
<img src="images/SAS_math_functions.png" alt="Some mathematical functions in SAS" /><p class="caption">Some mathematical functions in SAS</p>
</div>
<p>We can also do operations on strings, the following code replaces the variable &quot;Sex&quot; with the first entry of &quot;Sex&quot; (which gets rid of the Male - M and Female - F issue).</p>
<pre><code>data mat013.MMM_with_BMI;
set mat013.MMM;
sex=substr(sex,1,1);
run;</code></pre>
<div class="figure">
<img src="images/SAS_string_functions.png" alt="Some string functions in SAS" /><p class="caption">Some string functions in SAS</p>
</div>
<p>It's worth checking the web for a full list of various SAS functions (there are a huge amount of them).</p>
<h3 id="dropping-and-keeping-variables.">Dropping and keeping variables.</h3>
<p>In this section we'll take a quick look at two simple ways of improving the efficiency of a data step. Recalling how SAS handles a data step (using the pdv as described previously), one immediate way of improving efficiency is to ensure that the pdv only &quot;transports&quot; the variables we require. We do this with the &quot;drop&quot; or &quot;keep&quot; statement.</p>
<p>Let us consider the previous example and assume that we want our MMM_with_BMI data set without the weight and height variables. We use a &quot;drop&quot; statement to get rid of those variables:</p>
<pre><code>data mat013.MMM_with_BMI_nhw(drop=weight_in_kg height_in_metres);
set mat013.MMM;
bmi=weight_in_kg/(height_in_metres**2);
run;</code></pre>
<p>Note that the following code would not give the required output as we are trying to drop the variables from the original data set, however we need those variables to calculate the bmi:</p>
<pre><code>data mat013.MMM_with_BMI_nhw;
set mat013.MMM(drop=weight_in_kg height_in_metres);
bmi=weight_in_kg/(height_in_metres**2);
run;</code></pre>
<p>The keep statement (basically) does the same thing as the drop statement but in reverse, by only keeping the variables we have specified. Which one to use depends simply on whether or not you want to drop or keep more variables.</p>
<p>Note that you cannot use a drop statement and a keep statement in the same data step.</p>
<p>The following code will create a data set with just the bmi variable.</p>
<pre><code>data mat013.just_bmi(keep=bmi);
set mat013.MMM;
bmi=weight_in_kg/(height_in_metres**2);
run;</code></pre>
<h3 id="renaming-variables">Renaming variables</h3>
<p>The following code creates a data set &quot;JJJ&quot; in the work library which is a copy of the &quot;JJJ&quot; dataset in the mat013 library, renaming the &quot;sex&quot; variable to &quot;gender&quot;.</p>
<pre><code>data JJJ(rename=(sex=gender));
set mat013.JJJ;
run;</code></pre>
<p>This can also be used in the set data set:</p>
<pre><code>data JJJ;
set mat013.JJJ(rename=(sex=gender));
run;</code></pre>
<h3 id="operations-across-rows">Operations across rows</h3>
<p>We have seen in previous sections how to create new variables for any given observation (i.e. across columns of a data set). In this section we see how to create variables across rows. Recalling how the program data vector works, this implies that we must find a way to keep certain entries in the pdv for future calculation.</p>
<p>We will demonstrate this using the birthday_money.csv data set as shown.</p>
<div class="figure">
<img src="images/image38.png" alt="The birthday_money.csv data set." /><p class="caption">The birthday_money.csv data set.</p>
</div>
<p>The first such way is to use the &quot;retain&quot; statement. The &quot;retain&quot; statement keeps the last entry for a given variable in the pdv for future calculation. Note that we can give an initial value for a particular variable as shown in the following code (which produces a variable &quot;total&quot; that is a running total of &quot;amount&quot;) the output of which is shown.</p>
<pre><code>data bm_analysis;
set mat013.birthday_money;
retain total 0;
total=total+amount;
run;</code></pre>
<div class="figure">
<img src="images/image47.png" alt="A running total calculated using the retain statement" /><p class="caption">A running total calculated using the retain statement</p>
</div>
<p>Another tool for such calculations is the &quot;lagn&quot; function which gives the value of a variable from a certain number n of prior steps. The following code gives two new variables, the yearly difference and 2 yearly difference, the result of which is shown.</p>
<pre><code>data bm_analysis;
set mat013.birthday_money;
retain total 0;
total=total+amount;
yearly_diff=amount-lag1(amount);
two_yearly_diff=amount-lag2(amount);
run;</code></pre>
<div class="figure">
<img src="images/image40.png" alt="Yearly and 2 yearly differences calculated using the lag1 and lag2 functions." /><p class="caption">Yearly and 2 yearly differences calculated using the lag1 and lag2 functions.</p>
</div>
<p>The lag functions can be used in much more complex assignments and in fact when simply wanting to calculate a difference there is a quicker way: using the &quot;difn&quot; function as shown in the code below which gives the same result as shown.</p>
<pre><code>data bm_analysis;
set mat013.birthday_money;
retain total 0;
total=total+amount;
yearly_diff=dif1(amount);
two_yearly_diff=dif2(amount);
run;</code></pre>
<h2 id="handling-dates">3.4 Handling dates</h2>
<p>Dates are handled in a particular way in SAS. Let's consider the csv file shown.</p>
<div class="figure">
<img src="images/image37.png" alt="The birthdays csv file." /><p class="caption">The birthdays csv file.</p>
</div>
<p>We have seen in Chapter 1 how to import data using proc import. If we use the normal approach an error would occur. This is due to the confusion associated with our birthday variables (the first 20 rows have the date and month values both less than 12). A further option that can be incorporated in proc import is the number of rows that SAS will &quot;pre-read&quot; to identify the type of variables that are to be imported. This is often an easy way to ensure that SAS recognises dates.</p>
<pre><code>proc import datafile=&#39;\~birthdays.csv&#39;
out=birthdays
replace;
getnames=yes;
guessingrows=25;
run;</code></pre>
<p>A proc contents run on the above data set shows that the birthday variable data was imported using the informat DDMMYY10. In other words SAS has recognised that the dates were in that particular format.</p>
<div class="figure">
<img src="images/image50.png" alt="Proc contents on the birthdays file." /><p class="caption">Proc contents on the birthdays file.</p>
</div>
<p>Another approach is to import files in SAS using a data step and the infile statement. When doing this we can tell SAS the format of the data (whether or not it is a string, numerical or date variables).</p>
<pre><code>data birthdays;
infile &#39;~/birthdays.csv&#39; dlm=&#39;,&#39; firstobs=2;
input Name $ Birthday ddmmyy10.;
run;</code></pre>
<p>The infile statement tells SAS where the data is located and the 'dlm' statement tells SAS how the file is delimited (in this case with a comma). The 'firstobs' statement tells SAS where the data starts in the file (in this case the second row as the first row is the name of the variables in our data set). The input statement then allows us to tell SAS the names of the variables as well as the format they are in, here we tell SAS that the second variable is to be called 'Birthday' and it is in the ddmmyy8. format.</p>
<div class="figure">
<img src="images/image63.png" alt="The birthdays data set import using the infile statement." /><p class="caption">The birthdays data set import using the infile statement.</p>
</div>
<p>The above output might be a bit confusing, this is due to the fact that SAS handles dates as numbers, using the convention that the 1st of January 1960 is the number 0 (this allows for straightforward arithmetic manipulation of dates). The following code imports the data as above and displays the underlying numeric dates in the date9. format.</p>
<pre><code>data birthdays;
infile &#39;\~/birthdays.csv&#39; dlm=&#39;,&#39; firstobs=2;
input Name $ Birthday ddmmyy8.;
format Birthday date9.;
run;</code></pre>
<p>The output is shown. Note that applying the date9. format only changes the appearance of the data.</p>
<div class="figure">
<img src="images/image51.png" alt="The birthdays data set import using the infile statement." /><p class="caption">The birthdays data set import using the infile statement.</p>
</div>
<p>There are various formats that can be used when importing variables (for dates as well as other variables) and subsequently these same formats can be used to display the data if this is required. Searching online quickly finds other SAS formats.</p>
<hr />
<h1 id="chapter-4-programming">Chapter 4 Programming</h1>
<hr />
<h2 id="flow-control">4.1 Flow control</h2>
<p>A huge part of programming (in any language) is the use of so called &quot;conditional statements&quot;. We do this in SAS using &quot;if&quot; statements. The following code creates a new variable &quot;age_group&quot; which is &quot;young&quot; if the age is less than 29 and &quot;old&quot; if the age is larger than 29. Note we're also including a keep statement to just have the name and age_group in the new data set.</p>
<pre><code>data age_group(keep= name age_group);
set mat013.mmmjjj;
if age&lt;30 then age_group=&#39;young&#39;;
    else age_group=&#39;old&#39;;
run;</code></pre>
<p>We can also use this in conjunction with the else if statement as shown below:</p>
<pre><code>data age_group(keep= name age_group);
set mat013.mmmjjj;
if age&lt;18 then age_group=&#39;child&#39;;
    else if age&lt;30 then age_group=&#39;young&#39;;
        else age_group=&#39;old&#39;;
run;</code></pre>
<p>Note that we can also compare strings as shown with the following code:</p>
<pre><code>data age_group(keep= name age_group);
set mat013.mmmjjj;
if age&lt;18 then age_group=&#39;child&#39;;
    else if age&lt;30 then age_group=&#39;young&#39;;
        else age_group=&#39;old&#39;;
if substr(Name,1,1)=&#39;J &#39; then data_set=&#39;JJJ&#39;;
    else data_set=&#39;MMM&#39;;
run;</code></pre>
<p>Here are some of the comparison operators that can be used in conjunction with 'if' statements.</p>
<div class="figure">
<img src="images/image29.png" alt="SAS Comparison Operators." /><p class="caption">SAS Comparison Operators.</p>
</div>
<p>A further important notion in programming is the notion of loops. These are done in SAS using &quot;do&quot; statements. There are four ways the &quot;do&quot; statement is used:</p>
<ol style="list-style-type: decimal">
<li>do</li>
<li>do (iterative)</li>
<li>do while</li>
<li>do until</li>
</ol>
<p>The first use allows us to combine several statement into one. This is often used in conjunction with &quot;if&quot; statements:</p>
<pre><code>data age_group(keep= name age_group minor_Y_N);
set mat013.mmmjjj;
if age&lt;18 then do;
age_group=&#39;Child&#39;;
minor_Y_N=&#39;Y&#39;;
end;
else do;
age_group=&#39;Adult&#39;;
minor_Y_N=&#39;N&#39;;
end;
run;</code></pre>
<p>The 'do' statement can be used to push your computer a bit more. The &quot;do iterative statement&quot; allows you to automate various procedures. The following code output the total number of birthday candles that would have been used on everyones birthday cake in the JJJ data set.</p>
<pre><code>data candles(keep= name age candles);
set mat013.jjj;
candle=0;
do k=0 to age;
candle=candle+k;
end;
run;</code></pre>
<p>The last two uses of the 'do' statement are very similar and allow us to iterate &quot;until/while&quot; a particular condition is met.</p>
<p>The do until (expression) statement executes a group of statements until the expression within the brackets is satisfied. The validity of the expression is checked at the end of each loop.</p>
<pre><code>do until (expression);
data step commands;
end;</code></pre>
<p>The following code outputs the number of even numbers less than or equal to 70, computing each even number and checking whether or not it is more than 70.</p>
<pre><code>data even_numbers;
k=0;
even=0;
do until(even&gt;=70);
even=2**k;
k=k+1;
end;
run;</code></pre>
<p>We can do a similar calculation using the do &quot;while&quot; statement. The do while (expression) statement executes a group of statements whilst the expression within the brackets is satisfied. The validity of the expression is checked at the beginning of each loop.</p>
<pre><code>do while (expression);
data step commands;
end;

data even_numbers;
k=0;
even=0;
do while(even&lt;70);
even=2**k;
k=k+1;
end;
run;</code></pre>
<p>Note that do iterative statements (also called &quot;do loops&quot;) are often used in conjunction with the &quot;output&quot; statement which empties the pdv to the output data set. The following code outputs the variables in the pdv: &quot;k&quot; and &quot;even&quot; at each iteration of the do statement. The output is shown.</p>
<pre><code>data even_numbers;
k=0;
even=0;
do while(even&lt;70);
even=2**k;
output;
k=k+1;
end;
run;</code></pre>
<div class="figure">
<img src="images/image18.png" alt="The first even numbers less than 70." /><p class="caption">The first even numbers less than 70.</p>
</div>
<h2 id="how-does-sas-compile-code">4.2 How does SAS compile code?</h2>
<p>In this chapter we will see how to program macros in SAS. Macros generate and run code with varying arguments. The macro facility is a tool for extending and customising SAS and for reducing the amount of text you must enter to do common tasks. The macro facility enables you to assign a name to character strings or groups of SAS programming statements. From that point on, you can work with the names rather than with the text itself.</p>
<p>When you submit a SAS macro the Input stack receives content of the program. Word scanner scans each line of the macro for tokens. If a token contains a macro character (a % or a &amp;) that token is sent to the macro compiler. The Macro compiler does its work and places tokens back in the input stack. The token is examined by the word scanner and the process repeats. When the word scanner detects a step boundary it triggers the data step compiler. This process is represented diagrammatically.</p>
<div class="figure">
<img src="images/image48.png" alt="How SAS compiles code." /><p class="caption">How SAS compiles code.</p>
</div>
<p>When you submit a macro, it goes first to the macro processor which produces standard SAS code from the macro references (macro code is compiled first). Then SAS compiles and executes your program.</p>
<p>In general the syntax for a macro is as follows:</p>
<pre><code>%macro macro-name &lt;(macro-parameter-list&gt;;

â€¦ SAS Code...

%mend &lt;macro-name&gt;;</code></pre>
<p>The following example creates a macro called &quot;My_plot&quot; which when called will plot a graph of height against weight of the variables in mat013.jjj:</p>
<pre><code>%macro My_plot;
proc gplot data=mat013.jjj;
plot height_in_metres*weight_in_kg;
run;
%mend;</code></pre>
<p>To run the macro we call it with the following statement:</p>
<pre><code>%My_plot;</code></pre>
<p>As discussed above, it is possible to pass arguments to a macro. The following code creates a macro &quot;shopping&quot; that will remove a certain quantity &quot;spend&quot; from the variable &quot;life_savings&quot;:</p>
<pre><code>%macro shopping(spend);
data JJJ_after_shopping(keep= Name Old_savings New_savings);
set mat013.jjj;
Old_savings=savings_in_pounds;
New_savings=saving_in_pounds-&amp;spend;
run;
%mend;</code></pre>
<p>Note the ampersand &quot;&amp;&quot; which the &quot;word scanner&quot; will recognise, sending &quot;&amp;spend&quot; to the &quot;macro compiler&quot; where it will resolve to whatever value is passed to the macro.</p>
<p>We can define macros with multiple variables. Consider the following modification of the above code which allows for multiple shopping trips:</p>
<pre><code>%macro shopping(spend,trips);
data JJJ_after_shopping(keep= Name Old_savings New_savings);
set mat013.jjj;
Old_savings=savings_in_pounds;
New_savings=saving_in_pounds-&amp;trips*&amp;spend;
run;
%mend;</code></pre>
<p>The above code is using so called &quot;positional&quot; macro parameters. It is possible to also use &quot;keyword&quot; macro parameters as shown in the code below.</p>
<pre><code>%macro shopping(spend=,trips=);
data JJJ_after_shopping(keep= Name Old_savings New_savings);
set mat013.jjj;
Old_savings=savings_in_pounds;
New_savings=saving_in_pounds-&amp;trips*&amp;spend;
run;
%mend;</code></pre>
<p>We can then call the above macro and change the order of the parameters:</p>
<pre><code>%shopping(trips=2,spend=500);</code></pre>
<p>It's also possible to set default values:</p>
<pre><code>%macro shopping(spend=,trips=1);
data JJJ_after_shopping(keep= Name Old_savings New_savings);
set mat013.jjj;
Old_savings=savings_in_pounds;
New_savings=saving_in_pounds-&amp;trips*&amp;spend;
run;
%mend;</code></pre>
<p>Now if we call the macro without giving a value to trips it will take the default value 1.</p>
<pre><code>%shopping(spend=500);</code></pre>
<h3 id="macro-variables">Macro variables</h3>
<p>In this section we're going to take a slightly closer look at macro variables. A macro variable is a variable whose value is stored within the macro symbol table. When the macro variable is used in SAS code, SAS substitutes the value of the macro variable into the SAS code. SAS macro variables are distinguished by the &quot;&amp;&quot; sign before the variable name. Note that all SAS macro variables are stored as text strings.</p>
<p>We can experiment with macro variables using the %let statement which allows the construction of macro variables outside of a macro definition. This is the simplest form of a macro statement. It can be placed anywhere in a program, not only inside a Macro. &quot;%let&quot; creates global macro variables. An example of this is shown in the following code which gives the output shown.</p>
<pre><code>%let spend=400;
%let trips=500;

%macro shopping;
data JJJ_after_shopping(keep= Name Old_savings New_savings);
set mat013.jjj;
Old_savings=savings_in_pounds;
New_savings=saving_in_pounds-&amp;trips*&amp;spend;
run;
%mend;

%shopping;</code></pre>
<div class="figure">
<img src="images/image52.png" alt="Output of a macro using the %let statement." /><p class="caption">Output of a macro using the %let statement.</p>
</div>
<p>It's also possible to view (in the log) the values of a macro variable using the &quot;%put&quot; statement. There are two uses for it:</p>
<pre><code>%put &lt;text&gt; &amp;macro-variable-name;</code></pre>
<p>This outputs some <text> (optional) followed by the value of particular macro variable. The other use is shown below:</p>
<pre><code>%put Â &lt;_all_ | _global_ | _local_ &gt;;</code></pre>
<p>This will output either all, all the global or all the local macro variables. These statements should allow us to better understand some of the issues related to the resolution of multiple ampersands. Multiple ampersands can be used to allow the value of a macro variable to become another macro variable reference. The macro variable reference will be rescanned until the macro variable is resolved. There are 2 rules to follow:</p>
<ol style="list-style-type: decimal">
<li>&amp;&amp; is a token in its own right and resolves to &amp;</li>
<li>Each token is handled independently</li>
</ol>
<p>The important thing to note here is that a double ampersand &quot;&amp;&amp;&quot; is a token in itself that resolves to a single ampersand &quot;&amp;&quot; (THIS IS IMPORTANT).</p>
<div class="figure">
<img src="images/image44.png" alt="Understanding multiple ampersands." /><p class="caption">Understanding multiple ampersands.</p>
</div>
<div class="figure">
<img src="images/image43.png" alt="Understanding multiple ampersands" /><p class="caption">Understanding multiple ampersands</p>
</div>
<h2 id="sas-macro-programming-statements">4.3 SAS Macro programming statements</h2>
<p>The 'if' statements and 'do' loops discussed previously work in a very similar way to if statements and do loops within macros. The only modification is that these can be evaluated within the macro compiler before the entire submitted code is resolved. For this to work we need to use the &quot;%if&quot;, &quot;%then&quot; and &quot;%else&quot; statements when evaluating a conditional statement on a macro variable. The following code is an example of this:</p>
<pre><code>%macro shopping(spend,trips);
data JJJ_after_shopping(keep= Name Old_savings New_savings);
set mat013.jjj;
%if &amp;spend&lt;0 %then %put Carefull the spend is negative!;
%else %put The spend is positive;
Old_savings=savings_in_pounds;
New_savings=savings_in_pounds-&amp;trips*&amp;spend;
run;
%mend;</code></pre>
<p>The &quot;%do&quot; statement can be used in conjunction with &quot;%if&quot; statements. The following code creates one of two data sets depending on the sign of the macro variable spend.</p>
<pre><code>%macro shopping(spend,trips);
%if &amp;spend&lt;0 %then %do;
data JJJ_after_saving(keep= Name Old_savings New_savings);
set mat013.jjj;
%end;
%else %do;

data JJJ_after_spending(keep= Name Old_savings New_savings);
set mat013.jjj;
%end;

Old_savings=savings_in_pounds;
New_savings=savings_in_pounds-&amp;trips*&amp;spend;
run;

%mend;</code></pre>
<p>Another use of the %do statement is in iterative statements (as before). The difference being that on this occasion the %do statement creates macro variables. The following code creates various data sets each with a title indexed by a macro variable.</p>
<pre><code>%macro shopping(spend);
%do trips=1 %to 10;

data JJJ_after_saving_&amp;trips(keep= Name Old_savings New_savings);

set mat013.jjj;

Old_savings=savings_in_pounds;
New_savings=savings_in_pounds-&amp;trips*&amp;spend;

run;
%end;
%mend;</code></pre>
<p>The %do statement can also be used in conjunction with the %while and %until statements.</p>
<p>The way SAS compiles macro code can be an extremely useful tool. For example the following code creates a macro that imports 5 separate csv file:</p>
<pre><code>%macro import;
%do i=1 %to 5;
proc import datafile=&quot;\~/File_&amp;i.csv&quot;
    out=File_&amp;i
    dbms=csv
    replace;
    getnames=yes;
run;
%end;
%mend;</code></pre>
<p>The output is shown.</p>
<div class="figure">
<img src="images/image04.png" alt="Importing multiple data sets using macros." /><p class="caption">Importing multiple data sets using macros.</p>
</div>
<h2 id="macro-functions">4.4 Macro functions</h2>
<p>Since all macro variables are text strings it is not possible to directly perform computations on macro variables that contain numbers. The following code would give an error:</p>
<pre><code>%let var=5**2;

%put &amp;var;</code></pre>
<p>One must make use of the following function to be able to evaluate (in the macro compiler) such computations:</p>
<pre><code>%let var=5**2;

%put %eval(&amp;var);

%put %sysevalf(&amp;var);</code></pre>
<p>The &quot;%sysevalf&quot; function works in a very similar way to the &quot;%eval&quot; but will compute fractions such as 9/2 in the Real numbers (as opposed to eval which would round the result).</p>
<p>Another use of macro functions is when it comes to ignoring certain SAS keywords. The following code puts two different statements to the log.</p>
<pre><code>%let myvar=abc;
%put %str(this string is; &amp;myvar);
%put %nrstr(this string is; &amp;myvar %let);</code></pre>
<p>The first macro function &quot;%str&quot; ignores the &quot;;&quot; and treats it as a string. The second macro function &quot;nrstr&quot; ignores all the SAS statements including &quot;;,&amp;&quot; and &quot;%&quot;.</p>
<p>There are a large number of macro functions and it's worth looking around if you think there's one you might need. Also, of interest are the following commands (look them up) that can help with debugging:</p>
<ol style="list-style-type: decimal">
<li>mprint</li>
</ol>
<ul>
<li>writes all non-macro code generated by the macro</li>
</ul>
<ol start="2" style="list-style-type: decimal">
<li>mlogic</li>
</ol>
<ul>
<li>when a macro begins executing</li>
<li>values of macro parameters</li>
<li>when program statements execute</li>
<li>the status of any %if or %do condition</li>
<li>when a macro stops executing</li>
</ul>
<ol start="3" style="list-style-type: decimal">
<li>Symbolgen</li>
</ol>
<ul>
<li>writes information concerning the resolution of macro variables to the log</li>
</ul>
<hr />
<h1 id="chapter-5-further-procs">Chapter 5 Further procs</h1>
<hr />
<p>In this chapter we will examine three particular procedures in SAS.</p>
<ol style="list-style-type: decimal">
<li>proc sql: a procedure allowing for the use of sql syntax in SAS;</li>
<li>proc fcmp: a procedure allowing for the creation of custom functions;</li>
<li>proc optmodel: a package that allows for optimisation in SAS.</li>
</ol>
<h2 id="proc-sql">5.1 Proc sql</h2>
<h3 id="basic-sql">Basic SQL</h3>
<p>SQL is a language designed for querying and modifying databases. Used by a variety of database management software suites:</p>
<ol style="list-style-type: decimal">
<li>Oracle</li>
<li>Microsoft ACCESS</li>
<li>SPSS</li>
</ol>
<p>SQL uses one or more objects called TABLES where: rows contain records (observations) and columns contain variables. Importantly,</p>
<ol style="list-style-type: decimal">
<li>Starts with proc sql; (as expected)</li>
<li>Ends with quit; (some interactive procedures do)</li>
</ol>
<p>The following code creates a data set called test in the work library as a copy of the mat013.mmm data set:</p>
<pre><code>proc sql;
create table test as
select *
from mat013.mmm;
quit;</code></pre>
<p>The &quot;*&quot; command tells SAS to take all variables from mat013.mmm. We can however specify exactly what variables we want:</p>
<pre><code>proc sql;
create table test as
select Name, Age, Sex
from mat013.mmm;
quit;</code></pre>
<p>We can also create new variables:</p>
<pre><code>proc sql;
create table test as
select Name, Age, Sex, weight_in_kg/(height_in_metres**2) as bmi
from mat013.mmm;
quit;</code></pre>
<h3 id="further-sql">Further SQL</h3>
<p>In this section we'll take a look at what else SAS can do. For the purpose of the following examples let's write a new data set:</p>
<pre><code>data mat013.example;
input Var1 $ Var2 Var3 $ Var 4 Var5 $;
cards;
A 1 A 2 B
A 1 A 2 B
B 1 A 1 C
C 2 B 2 D
C 2 C 1 E
;
run;</code></pre>
<p>Some simple SQL code very easily helps us to get rid of duplicate rows (this can be very helpful when handling real data). To do this we use the &quot;distinct&quot; keyword.</p>
<pre><code>proc sql;
create table example as
select distinct *
from mat013.example;
quit;</code></pre>
<p>We can also select particular variables:</p>
<pre><code>proc sql;
create table example as
select distinct var1, var2, var3
from mat013.example;
quit;</code></pre>
<p>We can also use the &quot;where&quot; statement to select variables that obey a particular condition:</p>
<pre><code>proc sql;
create table example as
select *
from mat013.example
where var2&lt;=var4;
quit;</code></pre>
<p>We can sort data sets using the &quot;order by&quot; keyword:</p>
<pre><code>proc sql;
create table example as
select distinct *
from mat013.example
order by var1;
quit;</code></pre>
<p>A very nice application of SQL is in the aggregation of summary statistics. The following code creates a new variable that gives the average value of var2. The value of this variable is the same for all the observations:</p>
<pre><code>proc sql;
create table example as
select * mean(var2) as average_of_var2
from mat013.example;
quit;</code></pre>
<p>We could however get something a bit more useful by aggregating the data using a &quot;group&quot; statement:</p>
<pre><code>proc sql;
create table example as
select var1, mean(var2) as average_of_var2
from mat013.example
group by var1;
quit;</code></pre>
<h3 id="joining-tables-with-sql">Joining tables with SQL</h3>
<p>A very common use of SQL within SAS is to carry out &quot;joins&quot; which are equivalent to a merger of data sets. There are 4 types of joins to consider:</p>
<ol style="list-style-type: decimal">
<li>inner join
<ol style="list-style-type: decimal">
<li>output table only contains rows common to all tables</li>
<li>variable attributes taken from left most table</li>
</ol></li>
<li>outer join left
<ol style="list-style-type: decimal">
<li>output table contains all rows contributed by the left table</li>
<li>variable attributes taken from left most table</li>
</ol></li>
<li>outer join right
<ol style="list-style-type: decimal">
<li>output table contains all rows contributed by the right table</li>
<li>variable attributes taken from right most table</li>
</ol></li>
<li>outer join full
<ol style="list-style-type: decimal">
<li>output table contains all rows contributed by all tables</li>
<li>variable attributes taken from left most table</li>
</ol></li>
</ol>
<p>To work with these examples let's use the data sets created with the following code:</p>
<pre><code>data mat013.dogs;
input Owner $ Name $;
cards;
Jeff Ruffus
Janet Sam
Paul .
Joanna .
;
run;

data mat013.cats;
input Owner $ Name $;
cards;
Jeff Kitty
Paul .
Joanna Tinkerbell
Vince Chick
;
run;</code></pre>
<p>The following code carries out an inner join of these two datasets also changing the name of the &quot;Name&quot; variable depending on which data set it was from, the output of which is shown.</p>
<pre><code>proc sql;
create table merged_table as
select a.Owner,a.Name as Dog_Name, b.Name as cat_Name
from mat013.dogs as a, mat013.cats as b
where a.Owner=b.Owner;
quit;</code></pre>
<div class="figure">
<img src="images/image23.png" alt="The output of an inner join." /><p class="caption">The output of an inner join.</p>
</div>
<p>The following code carries out a left outer join, the output of which is shown.</p>
<pre><code>proc sql;
create table merged_table as
select a.Owner,a.Name as Dog_Name, b.Name as cat_Name
from mat013.dogs as a
left join mat013.cats as b
on a.Owner=b.Owner;
quit;</code></pre>
<div class="figure">
<img src="images/image46.png" alt="The output of a left outer join." /><p class="caption">The output of a left outer join.</p>
</div>
<p>The following code carries out a right outer join, the output of which is shown.</p>
<pre><code>proc sql;
create table merged_table as
select a.Owner,a.Name as Dog_Name, b.Name as cat_Name
from mat013.dogs as a
right join mat013.cats as b
on a.Owner=b.Owner;
quit;</code></pre>
<div class="figure">
<img src="images/image07.png" alt="The output of a right outer join." /><p class="caption">The output of a right outer join.</p>
</div>
<p>The following code carries out a full outer join, the output of which is shown.</p>
<pre><code>proc sql;
create table merged_table as
select a.Owner,a.Name as Dog_Name, b.Name as cat_Name
from mat013.dogs as a
full join mat013.cats as b
on a.Owner=b.Owner;
quit;</code></pre>
<div class="figure">
<img src="images/image33.png" alt="The output of a fullouter join." /><p class="caption">The output of a fullouter join.</p>
</div>
<h2 id="proc-fcmp">5.2 Proc fcmp</h2>
<p>In previous chapters we have seen various in built functions in SAS. For various reasons it might be required to create a custom function. We will do this with the &quot;fcmp&quot; procedure. This procedure allows us to create custom functions using data step syntax (which allows for &quot;if&quot; and &quot;do&quot; statements to be used). The following code creates a function called &quot;ln&quot; that gives the natural log of a number:</p>
<pre><code>proc fcmp outlib=sasuser.funcs.ln;
function ln(x);
y=log(x);
return(y);
endsub;
quit;</code></pre>
<p>This code in fact creates a function named &quot;ln&quot; in a package named &quot;funcs&quot;. The package is stored in the data set sasuser.funcs. To use this function we need to tell SAS which data set contains the function. We do this with the following piece of code:</p>
<pre><code>option cmplib=sasuser.funcs;</code></pre>
<p>It is then straightforward to call this function:</p>
<pre><code>option cmplib=sasuser.funcs;
data test;
x=5;
y=log(x);
new_Y=ln(x);
run;</code></pre>
<p>The main advantage to using this procedure is that we can include complex data step syntax. The following function takes two inputs and gives a geometric sum:</p>
<pre><code>proc fcmp outlib=sasuser.funcs.Gsum;
function Gsum(i,n);
s=0;
do k=0 to n;
s=s+i**k;
end;
return(s);
endsub;
quit;</code></pre>
<p>Let's test this on the following data set:</p>
<pre><code>data test;
input n i;
cards;
1 1
2 1
3 2
4 2
5 2
6 2
;
run;

data G_sum_test;
set test;
y=Gsum(i,n);
run;</code></pre>
<h2 id="optimisation">5.3 Optimisation</h2>
<p>Another powerful aspect of SAS is it's optimisation engine. We can optimise various types of problems using the &quot;optmodel&quot; procedure. The following code optimises the polynomial: <img style="vertical-align:middle" src="http://chart.apis.google.com/chart?cht=tx&amp;chl=x%5E2-x-yx%2By%5E2" alt="x^2-x-yx+y^2" title="x^2-x-yx+y^2" />.</p>
<pre><code>proc optmodel;
var x,y;
min z=x**2-x-2*y-x*y+y**2;
solve;
print x y;
quit;</code></pre>
<p>The output is shown, note that SAS automatically chooses a solver (in this case Non Linear Programming and Interior Point methods).</p>
<div class="figure">
<img src="images/image27_opt.png" alt="Output of the optmodel procedure." /><p class="caption">Output of the optmodel procedure.</p>
</div>
<p>We can also include a domain:</p>
<pre><code>proc optmodel;
var x&lt;=0,y&gt;=2;
min z=x**2-x-2*y-x*y+y**2;
solve;
print x y;
quit;</code></pre>
<div class="figure">
<img src="images/image22_opt.png" alt="Output of the optmodel procedure with a specific domain." /><p class="caption">Output of the optmodel procedure with a specific domain.</p>
</div>
<p>We can solve further more complex optimisation problems, including constraints using the 'constraints' keyword:</p>
<pre><code>proc optmodel;
var x1&gt;=0, x2&gt;=0, x3&gt;=0;
max f=x1+x2+x3;
constraint c1: 3*x1+2*x2-x3&lt;=1;
constraint c2: -2*x1-3*x2+2*x3&lt;=1;
solve;
print x1 x2 x3;
quit;</code></pre>
<p>The output is shown (note the solver used was a variant of simplex).</p>
<div class="figure">
<img src="images/image11_opt.png" alt="Output of the optmodel procedure for a problem including constraints." /><p class="caption">Output of the optmodel procedure for a problem including constraints.</p>
</div>
<p>It is also possible to read in the constraints of a particular optimisation problem from a data set. This can prove to be very handy when dealing with huge problems so it's worth spending time researching that approach.</p>
